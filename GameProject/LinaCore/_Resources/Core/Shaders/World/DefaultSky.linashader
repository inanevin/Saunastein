/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

/*

// Vertex input definition
struct LinaVertexInput
{
   vec4 pos;
   vec3 normal;
   vec2 uv;
};

// Vertex output definition
struct LinaVertexOutput
{
   vec4 clipPosition;
};

// Fragment output definition
struct LinaFragOutput
{
   vec4 color;
};

*/

#lina_shader_sky

#lina_variant
#name : Static
#vertex: Resources/Core/Shaders/Common/MainVertexStatic.linashader
#frag: Resources/Core/Shaders/Common/MainFragSimple.linashader
#pass: Resources/Core/Shaders/Common/RenderPass_Forward.linashader
#blend: LINA_BLEND_NONE
#depth: LINA_DEPTH_EQUAL
#depth_target: LINA_TARGET_D32
#target: LINA_TARGET_HDR
#cull: LINA_CULL_BACK
#lina_end

#lina_vs

layout (location = 0) out vec2 outUV;
layout (location = 1) out vec3 outNormal;
layout (location = 2) out vec3 outWorldPos;
layout (location = 3) out flat uint outMaterialIndex;

void LINA_VERTEX_MAIN(in LinaVertexInput inVertex, out LinaVertexOutput outVertex)
{
   mat4 rotView = mat4(mat3(LINA_VIEW.view));
   vec4 clipPos = LINA_VIEW.proj * rotView * inVertex.pos;
   outVertex.clipPosition = clipPos.xyww;
   outUV = inVertex.uv;
   outNormal = inVertex.normal;
   outWorldPos = inVertex.pos.xyz;
   outMaterialIndex = inVertex.drawArguments.argument1;
}

#lina_end

#lina_fs

layout (location = 0) in vec2 inUV;
layout (location = 1) in vec3 inNormal;
layout (location = 2) in vec3 inWorldPos;
layout (location = 3) in flat uint inMaterialIndex;

struct LinaMaterial
{
   vec3 skyColor;
   vec3 horizonColor;  
   vec3 groundColor;
   float horizonPosition;
   float horizonDiffusion;
   vec2 sunPosition;
   vec4 sunColor;
   float sunSize;
   float sunDiffusion;
};

#define DEBUG bool(0)

#define GRID_COLOR vec4(1.0,1.0,1.0,0.25)

vec4 AddGrid(vec3 col, vec3 p, float size, float thickness)
{
   vec2 sph = vec2(atan(p.x, p.z) / 3.141519 * 3.0, pow(abs(p.y) + 0.5, 2.0));

   sph *= size;
   float grid =      max(abs(fract(sph.y) - 0.5) * 2.0,
                     abs(fract(sph.x) - 0.5) * 2.0) *
                     (2.0 / thickness) - (2.0 / thickness - 0.75);

   sph /= 4.0;
   float thickGrid = max(abs(fract(sph.y) - 0.5) * 2.0,
                     abs(fract(sph.x) - 0.5) * 2.0) *
                     (2.0 / thickness) - (2.0 / thickness - 0.6);

   grid = clamp(max(grid, thickGrid), 0.0 ,1.0);
   grid *= 1.0-abs(p.y);

   return vec4(mix(col.rgb, 1.0 - col.rgb , grid * GRID_COLOR.a), 1.0);
}

/// --- Sky Shader --- ///
void LINA_FRAG_MAIN(out LinaFragOutput outFrag)
{
   LinaMaterial material = LINA_GET_MATERIAL(inMaterialIndex);

   vec2 uv = inUV;
   vec3 worldPos = inWorldPos;
   vec3 vNormal = normalize(inNormal);

   /// --- CLAMP PARAMETERS --- ///
   vec3 groundColor = clamp(material.groundColor, 0.0, 1.0);
   vec3 skyColor = clamp(material.skyColor, 0.0, 1.0);
   vec3 horizonColor = clamp(material.horizonColor, 0.0, 1.0);
   vec4 sunColor = clamp(material.sunColor, 0.0, 1.0);
   float sunSize = clamp(material.sunSize, 0.0, 1.0);
   float sunDiffusion = clamp(material.sunDiffusion, 0.2,16.0);


   /// --- HORIZON SHAPE --- ///
   float horizonShape = vNormal.y + material.horizonPosition;
   horizonShape = clamp(1.0 - abs(horizonShape / material.horizonDiffusion / 2.0), 0.0, 1.0);
   float horizonPos = worldPos.y - material.horizonPosition / 2.0;
 
   /// --- SUN SHAPE --- ///
   float sunShape = 1.0 - dot(vNormal , normalize(worldPos - vec3(material.sunPosition, 1.0)));
   sunShape = 1.0 / sunShape;
   sunShape = sunShape * material.sunSize * 0.01;
   sunShape = clamp(pow(sunShape, sunDiffusion), 0.0, 1.0);

   /// --- BLEND GROUND & SKY & HORIZON --- ///
   vec3 groundSky = mix(groundColor, skyColor, clamp(horizonPos * 1500., 0.0,1.0));
   vec3 worldColor = mix(groundSky, horizonColor,horizonShape);
   outFrag.color = vec4(mix(worldColor, sunColor.rgb, sunShape * sunColor.a), 1.0);

   /// --- Debug Grid -- ///
   outFrag.color = DEBUG ? AddGrid(outFrag.color.rgb, vNormal.xyz, 16.0, 0.1) : outFrag.color.rgba;
}

#lina_end

